#!/usr/bin/awk -f

# Purpose: From .csv input generate a series of DIV tags with lft/mid/rht classes for 3 column print output
# (Same output can be used for print or screen with the right .css file)

BEGIN { # Input uid,password,datestamp,seconds,usage,leaveat
	FS=","; # "," if directly using /tmp/usages.csv in listinactive-html, " " if directly using ./listusers
}

{
	class=(NR%3==2)?"mid":(NR%3==1)?"lft":"rht";
	sel=($1==uid)?"sel":"";
	frm=($6!=""?" from "$6:"");
	lea=($1==uid && stay>0)?"<a class=\"act\" href=\""list""ia"active.php?act=1&uid="uid"&stay="stay"\" title=\"Change leave time"frm"\">Until "leave"</a>":"";
	clea=($1==uid && stay>0)?($5>0 ?"(Cur "$5" Mb)":""):($5>0?"(Cur "$5" Mb, until "$6")":($6!=""?"(Until "$6")":""));
	print "<div class=\"base "class" box\">";
	print "  <div class=\"join "sel"\">";
	print "    <span class=\"print\">&nbsp;&nbsp;User: </span><b>"$1"</b>";
	print "  </div>";
	print "  <div class=\"join "sel"\">";
	print "    <span class=\"print\">Pass: </span><b>"$2"</b>";
	print "  </div>";
	print "  <div class=\"join buttons "sel"\">";
	if(buttons==1) {
		print "    <a href=\""list""ia"active.php?uid="$1"&stay="mstay"\" title=\"Reduce stay\">&nbsp;-&nbsp;&nbsp;</a>";
		print "    <a href=\""list""ia"active.php?uid="$1"&stay="pstay"\" title=\"Lengthen stay\">&nbsp;+&nbsp;</a>";
		print "    "lea;
	}
	if(buttons>0) {
		print "    <span class=\"small\" title=\"Current daily usage and leave time.\">"clea"</span>";
	}
	if(buttons!=1 && buttons!=2) {
		print "    &nbsp;";
	}
	print "  </div>";
	print "</div>";
};
